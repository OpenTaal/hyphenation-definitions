/* Hyphenation Definitions 0.8
 * https://raw.github.com/OpenTaal/hyphenation-definitions/master/
 * grammar/grammar.ebnf
 *
 * Reserved characters
 * tab                         U+0009  CHARACTER TABULATION  '\t'
 * line feed                   U+000A  LINE FEED (LF)        '\n'
 * carriage return             U+000D  CARRIAGE RETURN (CR)  '\r'
 * space                       U+0020  SPACE                 ' '
 * begin comment               U+0023  NUMBER SIGN           '#'
 * unfavourable hyphen         U+002E  FULL STOP             '.'
 * cluster separator           U+002F  SOLIDUS               '/'
 * delimiter                   U+003B  SEMICOLON             ';'
 * compound hyphen             U+003D  EQUALS SIGN           '='
 * begin homograph cluster     U+005B  LEFT SQUARE BRACKET   '['
 * end homograph cluster       U+005D  RIGHT SQUARE BRACKET  ']'
 * begin substitution cluster  U+007B  LEFT CURLY BRACKET    '{'
 * prefix hyphen               U+007C  VERTICAL LINE         '|'
 * end substitution cluster    U+007D  RIGHT CURLY BRACKET   '}'
 * morpheme hyphen             U+007E  TILDE                 '~'
 * suffix hyphen               U+00A6  BROKEN BAR            '¦'
 */
HyphenationDefinitions
         ::= ( EOL* HyphenationDefinition? WhiteSpace? Comment? )*
EOL
         ::= ( '\r' | #x000D ) ( '\n' | #x000A )?
           | ( '\n' | #x000A )
WhiteSpace
         ::= ( ( ' ' | #x0009 )
             | ( '\t' | #x0020 ) )+
Comment
         ::= '#' ( [#x0009]
                 | [#x0020-#xD7FF]
                 | [#xE000-#xFFFD]
                 | [#x10000-#x10FFFF] )*
HyphenationDefinition
         ::= Word Delimiter Definition
Delimiter
         ::= ';' | #x003B
Word
         ::= Character Character+
Character
         ::= [#x0021-#x0022]
           | [#x0024-#x002D]
           | [#x0030-#x003A]
           | [#x003C]
           | [#x003E-#x005A]
           | [#x005C]
           | [#x005E]
           | [#x0060-#x007A]
           | [#x007F-#x00A5]
           | [#x00A7-#xD7FF]
           | [#xE000-#xFFFD]
           | [#x10000-#x10FFFF]
Definition
         ::= Cluster ( Hyphen Cluster )*
Hyphen
         ::= MorphemeHyphen
           | SuffixHyphen
           | PrefixHyphen
           | CompoundHyphen
           | CompoundSuffixHyphen
           | CompoundPrefixHyphen
           | UnfavourableHyphen
MorphemeHyphen
         ::= ( '~' | #x007E )+
SuffixHyphen
         ::= '¦' | #x00A6
PrefixHyphen
         ::= '|' | #x007C
CompoundHyphen
         ::= ( '=' | #x003D )+

CompoundSuffixHyphen
         ::= ( '=' | #x003D )+ ( '¦' | #x00A6 )

CompoundPrefixHyphen
         ::= ( '|' | #x007C ) ( '=' | #x003D )+

UnfavourableHyphen
         ::= ( ( '~' | #x007E )
             | ( '|' | #x007C )
             | ( '¦' | #x00A6 )
             | ( '=' | #x003D ) )?
             ( '.' | #x002E )+
Cluster
         ::= ( CharacterCluster
             | SubstitutionCluster
             | HomographCluster )+
CharacterCluster
         ::= Character+
SubstitutionCluster
         ::= '{' CharacterCluster '/'
               ( CharacterCluster ( Hyphen CharacterCluster? )?
               | Hyphen CharacterCluster? )
             '}'
Series
         ::= ( CharacterCluster (Hyphen CharacterCluster)* Hyphen? )
           | ( Hyphen (CharacterCluster Hyphen)* CharacterCluster? )
HomographCluster
         ::= '[' ( Series | (SubstitutionCluster Series? ) ) '/'
                 SubstitutionCluster? Series? ']'
